{Application 'SHOWCASE' logic file generated by CSPro}
PROC GLOBAL

audio background;

PROC BARCODE_NO

PROC SHOWCASE_FF


PROC SIGNATURE

onfocus

string signature_file = maketext("%v-signature.png", HOUSEHOLD_ID);
valueset vs;

if fileexist(signature_file) then
     vs.add("Re-sign", 1);
     vs.add("View signature", 2);
     vs.add("Clear signature", 3);
     vs.add("Next question", 4);
else
     vs.add("Sign", 1);
endif;

setvalueset($, vs);

postproc

if $ = 1 then
	// Sign/re-sign
	execsystem("signature:" + pathconcat(application, signature_file));
	reenter;
elseif $ = 2 then
	// View signature
	execsystem("view:" + pathconcat(application, signature_file));
	reenter;
elseif $ = 3 then
	// Delete signature
    filedelete(signature_file);
    reenter;
elseif $ = 4 then
    // Continue to next question
endif;


PROC SCAN

onfocus

valueset vs;

if BARCODE_NO <> "" then
     vs.add("Re-scan", 1);
     vs.add("View barcode number", 2);
     vs.add("Clear barcode", 3);
     vs.add("Next question", 4);
else
     vs.add("Scan", 1);
endif;

setvalueset($, vs);

postproc

if $ = 1 then
	// Scan/re-scan barcode
	BARCODE_NO = barcode.read("Scan the beverage");
	reenter;
elseif $ = 2 then
	// View barcode number
    errmsg("Barcode number: %s", BARCODE_NO);
    reenter;
elseif $ = 3 then
	// Delete barcode number
    BARCODE_NO = "";
    reenter;
elseif $ = 4 then
    // Continue to next question
endif;


PROC INTERACTIVE_AUDIO

onfocus

audio recording;
string audio_file = maketext("%v-interactive-audio.m4a", HOUSEHOLD_ID);
valueset vs;

if fileexist(audio_file) and recording.load(audio_file) then
     vs.add("Re-record", 1);
     vs.add("Play recording", 2);
     vs.add("Clear recording", 3);
     vs.add("Next question", 4);
else
     vs.add("Record", 1);
endif;

setvalueset($, vs);

postproc

if $ = 1 then

    // Record/re-record
	recording.clear();
	string message = "Record the respondents answer to this question";
	numeric seconds = recording.recordinteractive(message);
	if seconds > 0 then
	 recording.save(audio_file);
	else
	 errmsg("No audio recorded. Please try again");
	endif;
	reenter;
	
elseif $ = 2 then
    // Play back recording
     recording.play();
     reenter;
elseif $ = 3 then
    // Clear the recording
     filedelete(audio_file);
     reenter;
elseif $ = 4 then
    // Continue to next question
endif;


PROC BACKGROUND_AUDIO_1

preproc

	// 600 s = 10 min
	background.clear();
	background.record(600);


PROC BACKGROUND_AUDIO_2

postproc

	background.stop();
	string audio_file = maketext("%v-background-audio.m4a", HOUSEHOLD_ID);
	background.save(audio_file);

PROC BACKGROUND_AUDIO_PLAYBACK

onfocus

audio recording;
string audio_file = maketext("%v-background-audio.m4a", HOUSEHOLD_ID);
valueset vs;

if fileexist(audio_file) and recording.load(audio_file) then
    vs.add("Play recording", 2);
    vs.add("Clear recording", 3);
    vs.add("Next question", 4);
else
	vs.add("Next question", 4);
endif;

setvalueset($, vs);

postproc

if $ = 1 then
	// Record/re-record
	recording.clear();
	errmsg("Reentering one field before background audio");
	reenter INTERACTIVE_AUDIO;
elseif $ = 2 then
    // Play back recording
     recording.play();
     reenter;
elseif $ = 3 then
    // Clear the recording
     filedelete(audio_file);
     reenter;
elseif $ = 4 then
    // Continue to next question
endif;
